/*
 * Copyright © 2024 Museum für Naturkunde Berlin.
 * This code is licensed under MIT license (see LICENSE.txt for details)
 */

plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'androidx.navigation.safeargs.kotlin'
    id 'kotlin-parcelize'
    id 'org.jetbrains.kotlin.plugin.serialization' version "1.9.20"
    id 'org.jlleitschuh.gradle.ktlint' version '10.2.1'
    id 'berlin.mfn.naturblick.strapidbgenerator' version '1.0.0'
}

def version = new Properties()
file("../version.properties").withInputStream { version.load(it) }

android {
    compileSdk 34

    defaultConfig {
        applicationId "com.mfn_berlin_stadtnatur_entdecken.naturblick"
        minSdk 21
        targetSdk 34
        versionCode Integer.parseInt(version.getProperty("version.code"))
        versionName version.getProperty("version.name")
        def strapiBaseUrl = "https://naturblick.museumfuernaturkunde.berlin/strapi/"
        syncReleaseMedia.strapiBaseUrl = strapiBaseUrl
        def ktorBaseUrl = "https://naturblick.museumfuernaturkunde.berlin/"
        syncDebugMedia.ktorBaseUrl = ktorBaseUrl
        syncReleaseDb.ktorBaseUrl = ktorBaseUrl
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField("String", "STRAPI_URL", "\"$strapiBaseUrl\"")
        buildConfigField("String", "BACKEND_URL", "\"https://naturblick.museumfuernaturkunde.berlin/api/\"")
        buildConfigField("String", "ANALYTICS_URL", "\"https://naturblick.museumfuernaturkunde.berlin/analytics\"")
        buildConfigField("String", "STYLE_URL", "\"mapbox://styles/stadtnaturentdecken/ckhg9nswa19dy19npe3nb5uia\"")
        resValue("string", "mapbox_access_token", providers.gradleProperty("MAPBOX_ACCESS_TOKEN").getOrElse(System.getenv("MAPBOX_ACCESS_TOKEN")))
        resourceConfigurations += ['en', 'de']
        javaCompileOptions {
            annotationProcessorOptions {
                 arguments += ["room.schemaLocation":
                             "$projectDir/schemas".toString()]
            }
        }

    }
    signingConfigs {
        release {
            keyAlias "upload"
            keyPassword System.env.KEYSTORE_PASSWORD
            storeFile file("../upload-keystore.jks")
            storePassword System.env.KEYSTORE_PASSWORD
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        getByName("debug") {
            applicationIdSuffix = ".debug"
            def strapiBaseUrl = "https://staging.naturblick.museumfuernaturkunde.berlin/strapi/"
            syncDebugMedia.strapiBaseUrl = strapiBaseUrl
            def ktorBaseUrl = "https://staging.naturblick.museumfuernaturkunde.berlin/"
            syncDebugMedia.ktorBaseUrl = ktorBaseUrl
            syncDebugDb.ktorBaseUrl = ktorBaseUrl
            buildConfigField("String", "BACKEND_URL", "\"https://staging.naturblick.museumfuernaturkunde.berlin/api/\"")
            buildConfigField("String", "ANALYTICS_URL", "\"https://staging.naturblick.museumfuernaturkunde.berlin/analytics/\"")
            buildConfigField("String", "STRAPI_URL", "\"$strapiBaseUrl\"")
        }
    }
    compileOptions {
        // Flag to enable support for java 1.8 APIs
        coreLibraryDesugaringEnabled true

        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    buildFeatures {
        dataBinding true
        buildConfig true
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion = "1.5.15"
    }
    sourceSets {
        main.assets.srcDirs += 'build/assets'
        main.res.srcDirs += 'build/generated/res'
    }
    namespace 'berlin.mfn.naturblick'
    applicationVariants.all { variant ->
        tasks["merge${variant.name.capitalize()}Assets"]
            .dependsOn(tasks["sync${variant.name.capitalize()}Db"])
        tasks["generate${variant.name.capitalize()}ResValues"]
            .dependsOn(tasks["sync${variant.name.capitalize()}Media"])
        tasks["map${variant.name.capitalize()}SourceSetPaths"].dependsOn(tasks["merge${variant.name.capitalize()}Resources"])
        tasks["sync${variant.name.capitalize()}Media"].imageDirectory = file("$buildDir/generated/res/drawable")
    }
}

ktlint {
    android.set(true)
    disabledRules.set(["no-wildcard-imports"])
}

dependencies {
    def composeBom = platform('androidx.compose:compose-bom:2024.09.00')
    implementation composeBom
    androidTestImplementation composeBom
    implementation 'androidx.compose.material:material'
    implementation 'androidx.activity:activity-compose'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-compose'
    // Android Studio Preview support
    implementation 'androidx.compose.ui:ui-tooling-preview'
    debugImplementation 'androidx.compose.ui:ui-tooling'

    implementation 'androidx.core:core-ktx:1.13.1'
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.8.3'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.8.3'
    implementation "androidx.navigation:navigation-fragment-ktx:2.7.7"
    implementation "androidx.navigation:navigation-ui-ktx:2.7.7"

    // Version 3.3.0 have a bug that crashes the recycler view in the observations list
    implementation "androidx.paging:paging-runtime-ktx:3.2.1"
    implementation "androidx.paging:paging-compose:3.2.1"
    implementation 'com.google.android.gms:play-services-location:21.3.0'
    implementation "androidx.savedstate:savedstate-ktx:1.2.1"
    implementation 'com.github.bumptech.glide:glide:4.12.0'
    implementation 'com.github.bumptech.glide:compose:1.0.0-beta01'

    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.jakewharton.retrofit:retrofit2-kotlinx-serialization-converter:0.8.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.3.1'

    implementation 'com.squareup.okhttp3:logging-interceptor:4.7.2'
    implementation "androidx.room:room-runtime:2.6.1"
    implementation "androidx.room:room-ktx:2.6.1"
    implementation "androidx.room:room-paging:2.6.1"
    implementation("androidx.work:work-runtime-ktx:2.9.0")


    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.mapbox.maps:android:11.6.1'
    implementation 'com.mapbox.extension:maps-compose:11.6.1'
    implementation 'com.github.CanHub:Android-Image-Cropper:4.3.0'
    implementation 'com.github.razir.progressbutton:progressbutton:2.1.0'
    implementation "androidx.exifinterface:exifinterface:1.3.7"

    implementation "androidx.media3:media3-exoplayer:1.3.1"

    // Snowplow Android Tracker
    implementation 'com.snowplowanalytics:snowplow-android-tracker:4.0.0'

    //noinspection GradleDynamicVersion
    testImplementation 'junit:junit:4.+'

    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:2.0.4"

    kapt 'com.github.bumptech.glide:compiler:4.12.0'
    kapt "androidx.room:room-compiler:2.6.1"

    implementation 'com.github.wendykierp:JTransforms:3.1'
}
