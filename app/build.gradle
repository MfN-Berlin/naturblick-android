/*
 * Copyright © 2024 Museum für Naturkunde Berlin.
 * This code is licensed under MIT license (see LICENSE.txt for details)
 */

plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'androidx.navigation.safeargs.kotlin'
    id 'kotlin-parcelize'
    id 'org.jetbrains.kotlin.plugin.serialization' version "2.0.21"
    id 'org.jlleitschuh.gradle.ktlint' version '10.2.1'
    id 'berlin.mfn.naturblick.strapidbgenerator' version '1.0.0'
    id 'org.jetbrains.kotlin.plugin.compose' version "2.0.21"
    id 'androidx.room' version "2.7.2"
    id 'com.google.devtools.ksp' version '2.0.21-1.0.27'
}

def version = new Properties()
file("../version.properties").withInputStream { version.load(it) }

android {
    compileSdk 35

    defaultConfig {
        applicationId "com.mfn_berlin_stadtnatur_entdecken.naturblick"
        minSdk 21
        targetSdk 35
        versionCode Integer.parseInt(version.getProperty("version.code"))
        versionName version.getProperty("version.name")
        def strapiBaseUrl = "https://naturblick.museumfuernaturkunde.berlin/strapi/"
        syncReleaseMedia.strapiBaseUrl = strapiBaseUrl
        def djangoBaseUrl = "https://naturblick.museumfuernaturkunde.berlin/"
        syncReleaseMedia.djangoBaseUrl = djangoBaseUrl + "django/"
        syncReleaseDb.djangoBaseUrl = djangoBaseUrl + "django/"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField("String", "STRAPI_URL", "\"$strapiBaseUrl\"")
        buildConfigField("String", "DJANGO_URL", "\"$djangoBaseUrl\"")
        buildConfigField("String", "BACKEND_URL", "\"https://naturblick.museumfuernaturkunde.berlin/api/\"")
        buildConfigField("String", "ANALYTICS_URL", "\"https://naturblick.museumfuernaturkunde.berlin/analytics\"")
        buildConfigField("String", "STYLE_URL", "\"mapbox://styles/stadtnaturentdecken/ckhg9nswa19dy19npe3nb5uia\"")
        resValue("string", "mapbox_access_token", providers.gradleProperty("MAPBOX_ACCESS_TOKEN").getOrElse(System.getenv("MAPBOX_ACCESS_TOKEN")))
        resourceConfigurations += ['en', 'de']
        room {
            schemaDirectory "$projectDir/schemas"
        }
    }
    signingConfigs {
        release {
            keyAlias "upload"
            keyPassword System.env.KEYSTORE_PASSWORD
            storeFile file("../upload-keystore.jks")
            storePassword System.env.KEYSTORE_PASSWORD
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        getByName("debug") {
            applicationIdSuffix = ".debug"
            def strapiBaseUrl = "https://staging.naturblick.museumfuernaturkunde.berlin/strapi/"
            syncDebugMedia.strapiBaseUrl = strapiBaseUrl
            def djangoBaseUrl = "https://staging.naturblick.museumfuernaturkunde.berlin/"
            syncDebugMedia.djangoBaseUrl = djangoBaseUrl + "django/"
            syncDebugDb.djangoBaseUrl = djangoBaseUrl + "django/"
            buildConfigField("String", "BACKEND_URL", "\"https://staging.naturblick.museumfuernaturkunde.berlin/api/\"")
            buildConfigField("String", "ANALYTICS_URL", "\"https://staging.naturblick.museumfuernaturkunde.berlin/analytics/\"")
            buildConfigField("String", "STRAPI_URL", "\"$strapiBaseUrl\"")
            buildConfigField("String", "DJANGO_URL", "\"$djangoBaseUrl\"")
        }
    }
    compileOptions {
        // Flag to enable support for java 1.8 APIs
        coreLibraryDesugaringEnabled true

        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    buildFeatures {
        dataBinding true
        buildConfig true
        compose true
    }
    sourceSets {
        main.assets.srcDirs += 'build/assets'
        main.res.srcDirs += 'build/generated/res'
    }
    namespace 'berlin.mfn.naturblick'
    applicationVariants.all { variant ->
        tasks["merge${variant.name.capitalize()}Assets"]
            .dependsOn(tasks["sync${variant.name.capitalize()}Db"])
        tasks["generate${variant.name.capitalize()}ResValues"]
            .dependsOn(tasks["sync${variant.name.capitalize()}Media"])
        tasks["map${variant.name.capitalize()}SourceSetPaths"].dependsOn(tasks["merge${variant.name.capitalize()}Resources"])
        tasks["sync${variant.name.capitalize()}Media"].imageDirectory = file("$buildDir/generated/res/drawable")
    }
}

ktlint {
    android.set(true)
    disabledRules.set(["no-wildcard-imports"])
}

dependencies {
    def composeBom = platform('androidx.compose:compose-bom:2025.07.00')
    implementation composeBom
    androidTestImplementation composeBom
    implementation 'androidx.compose.material:material'
    implementation 'androidx.activity:activity-compose'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-compose'
    implementation 'androidx.compose.ui:ui-viewbinding'
    implementation 'androidx.compose.material:material-icons-core'
    implementation 'androidx.fragment:fragment-compose:1.8.8'
    // Android Studio Preview support
    implementation 'androidx.compose.ui:ui-tooling-preview'
    debugImplementation 'androidx.compose.ui:ui-tooling'

    implementation 'androidx.core:core-ktx:1.16.0'
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.9.2'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.9.2'
    implementation "androidx.navigation:navigation-fragment-ktx:2.9.3"
    implementation "androidx.navigation:navigation-ui-ktx:2.9.3"

    implementation "androidx.paging:paging-runtime-ktx:3.3.6"
    implementation "androidx.paging:paging-compose:3.3.6"
    implementation 'com.google.android.gms:play-services-location:21.3.0'
    implementation 'com.github.bumptech.glide:glide:4.16.0'
    implementation 'com.github.bumptech.glide:compose:1.0.0-beta01'

    implementation 'com.squareup.retrofit2:retrofit:3.0.0'
    implementation 'com.jakewharton.retrofit:retrofit2-kotlinx-serialization-converter:1.0.0'

    // kotlinx-serialization-json 1.9.0 or logging-interceptor 5.1.0 crashes during compilation
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.8.1'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.12.0'

    implementation "androidx.room:room-runtime:2.7.2"
    implementation "androidx.room:room-ktx:2.7.2"
    implementation "androidx.room:room-paging:2.7.2"
    implementation("androidx.work:work-runtime-ktx:2.10.3")


    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.mapbox.maps:android-ndk27:10.19.0'
    implementation 'com.github.CanHub:Android-Image-Cropper:4.4.0'
    implementation 'com.github.razir.progressbutton:progressbutton:2.1.0'
    implementation "androidx.exifinterface:exifinterface:1.4.1"

    implementation "androidx.media3:media3-exoplayer:1.8.0"

    // E-Mail intent builder for feedback email
    implementation 'de.cketti.mailto:email-intent-builder:2.0.0'

    // Snowplow Android Tracker
    implementation 'com.snowplowanalytics:snowplow-android-tracker:4.0.0'

    //noinspection GradleDynamicVersion
    testImplementation 'junit:junit:4.+'

    androidTestImplementation 'androidx.test.ext:junit:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.7.0'

    coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:2.1.5"

    ksp 'com.github.bumptech.glide:ksp:4.16.0'
    ksp "androidx.room:room-compiler:2.7.2"

    implementation 'com.github.wendykierp:JTransforms:3.1'
}
